import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
}
apply from: 'gradle/properties.gradle'

group = info.group
version = "${info.version}-${versions.spigot}"

sourceCompatibility = 18
targetCompatibility = 18

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = uri('https://maven.pkg.github.com/m4kvn/spigot-nms')
        credentials {
            username = github.username
            password = github.password
        }
    }
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot:${versions.spigot}"
    compileOnly "org.apache.logging.log4j:log4j-core:2.19.0"
    compileOnly "org.yaml:snakeyaml:1.33"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.github.m4kvn:spigot-nms:1.1.0"
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

jar {
    exclude "META-INF/versions/9/module-info.class"
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}

task copyJarToServer(type: Copy) {
    from jar
    into "server/plugins"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

build.dependsOn copyJarToServer